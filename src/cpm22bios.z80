ft245: equ 8
ft245_status: equ 9
bell: equ 6
sd_data: equ 10
sd_status: equ 11

msize: equ 62

bias: equ (msize - 20) * 1024
ccp: equ $3400 + bias
bdos: equ ccp + $806
bios: equ ccp + $1600
cdisk: equ $0004
iobyte: equ $0003

nsects: equ ((bios - ccp) / 128)

.macro sd_set_cs
    ld a,%%1
    out (sd_status),a   ; set cs to 0
.endm

.phase bios

    jp boot
wboote:
    jp wboot
    jp const
    jp conin
    jp conout
    jp list
    jp punch
    jp reader
    jp home
    jp seldsk
    jp settrk
    jp setsec
    jp setdma
    jp read
    jp write
    jp listst
    jp sectran

boot:
    ld sp,$0080
    call init_sd_card
    xor a
    ld (iobyte),a   ; set iobyte to 0
    ld (cdisk),a    ; set current disk to 0
    jp gocpm
bootmsg:
    db "CP/M 2.2 62K\n$"
wboot:
    ; need to read nsects sectors from disk into ccp
    ld sp,$0080
    ld de,bootmsg
    ld c,9
    call bdos
    call init_sd_card
    ld c,0
    call seldsk     ; select drive A:
    call home       ; set track to 0
    ld b,nsects     ; set count to nsects
    ld c,0          ; set c to be track 0
    ld de,$0002     ; start on sector 2, to skip the potential boot sectors
    ld hl,ccp       ; set destination to ccp
load1:
    push bc         ; store count and track
    push de         ; store sector
    push hl         ; store destination
    ld c,d
    ld b,e
    call setsec     ; set sector to the current one
    pop bc          ; get destination in bc
    push bc
    call setdma     ; set dma address so that data is loaded into right place
    call read       ; load data
    cp 0            ; check error code
    jp nz,wboot     ; if error, try again!
    pop hl          ; get destination again
    ld de,128       ; add 1 sector
    add hl,de
    pop de          ; pop sector
    pop bc          ; pop count and track
    dec b           ; decrement count
    jp z,gocpm      ; if all read, start cpm
    inc d           ; increment sector
    jp load1        ; load some more

gocpm:
    ld a,$c3        ; c3 is jp instr
    ld ($0000),a    ; store 'jp wboot' at 0
    ld hl,wboote
    ld ($0001),hl

    ld ($0005),a    ; store 'jp bdos' at 5
    ld hl,bdos
    ld ($0006),hl

    ld bc,$0080       ; set dma to $80
    call setdma

    ei              ; enable interrupts
    ld a,(cdisk)    ; load current disk
    ld c,a
    jp ccp          ; start the ccp

init_sd_card:
    sd_set_cs 1
    ld b,10
init:
    ld a,$ff
    call sd_write_a
    djnz init

.block
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait
.endblock

    sd_set_cs 0

    ld hl,cmd0
    call sd_send_command_slow
    ld (response),a

    sd_set_cs 1

    sd_set_cs 0

    ld hl,cmd8
    call sd_send_command_slow
    ld (response),a

    ld b,4
    ld hl,response
    call sd_read_bytes

    sd_set_cs 1

    ; should check responses are correct, in proper sd card driver

send_acmd41:
    sd_set_cs 0
    ld hl,acmd
    call sd_send_command_slow

    ld hl,acmd41
    call sd_send_command_slow
    ld (response),a

    sd_set_cs 1

    ld a,(response)
    cp 0
    jp nz,send_acmd41

    sd_set_cs 0
    ld hl,cmd58
    call sd_send_command_slow

    ld b,4
    ld hl,response
    call sd_read_bytes

    sd_set_cs 1
    ret

const:
    in a,(ft245_status)
    bit 0,a
    jp z,const_ready
    ld a,0
    ret
const_ready:
    ld a,$ff
    ret

conin:
    in a,(ft245_status)
    bit 0,a
    jp nz,conin
    in a,(ft245)
    res 7,a
    ret

conout:
    in a,(ft245_status)
    bit 1,a
    jp nz,conout
    ld a,c
    out (ft245),a
    ret

list:
    ret
punch:
    ret
reader:
    ret
    
home:
    ld bc,0
    ld (track),bc
    ret

seldsk:
    ld hl,0
    xor a       ; only one disk - return error if c isn't 0
    cp c
    ret nz

    ld hl,disk
    ld (hl),c
    ld hl,dpb_disk0
    ret

settrk:
    ld (track),bc
    ret

setsec:
    ld (sector),bc
    ret

setdma:
    ld (dma_addr),bc
    ret

read:
    ld bc,(sector)
    ld a,b
    ld (cmd_read_block + 3),a
    ld a,c
    ld (cmd_read_block + 4),a
    ld bc,(track)
    ld a,b
    ld (cmd_read_block + 1),a
    ld a,c
    ld (cmd_read_block + 2),a
    ; ld a,(disk)
    ; ld (cmd_read_block + 2),a

    sd_set_cs 2
    ld hl,cmd_read_block
    call sd_send_command
wait_for_data:
    ld a,$ff
    call sd_write_a
.block
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait
.endblock
    in a,(sd_data)
    cp $fe
    jp nz,wait_for_data

    ld b,0
    ; ld hl,block0
    ld hl,buffer
    call sd_read_bytes
    ld b,0
    ; ld hl,block1
    ld hl,buffer + $100
    call sd_read_bytes

    sd_set_cs 3
    ld de,(dma_addr)
    ld bc,$80
    ld hl,buffer
    ldir

    ld a,0  ; error code - should really check!!

    ret
write:
    ld bc,(sector)
    ld a,b
    ld (cmd_write_block + 3),a
    ld a,c
    ld (cmd_write_block + 4),a
    ld bc,(track)
    ld a,b
    ld (cmd_write_block + 1),a
    ld a,c
    ld (cmd_write_block + 2),a
    ; ld a,(disk)
    ; ld (cmd_write_block + 2),a

    ld hl,(dma_addr)
    ld bc,$80
    ld de,buffer
    ldir

    sd_set_cs 2
    ld hl,cmd_write_block
    call sd_send_command

    ld a,$fe  ; start block token
    call sd_write_a

    ld b,0
    ld hl,buffer
    call sd_write_bytes

    ld b,0
    ld hl,buffer + $100
    call sd_write_bytes

    call sd_read_data_response
    ld (response),a

    call sd_wait_for_non_busy_token
    sd_set_cs 3

    ld a,0

    ret
listst:
    ld a,0
    ret

sectran:
    ld h,b
    ld l,c
    ret

disk: db 0
track: dw 0
sector dw 0
dma_addr: dw $0080

;works
dpblk:
    dw $0100    ; SPT sectors per track
    db $04      ; BSH block shift factor
    db $0f      ; BLM block mask
    db $00      ; EXM extent mask
    dw $0fff    ; DSM disk size - 1
    dw $0100    ; DRM directory max
    db $f0      ; AL0 alloc 0
    db $00      ; AL1 alloc 1
    dw $0000    ; CKS check size - number of dir entries to check to see
                ;     if disk has changed. Treat this as a fixed disk for now.
    dw $0001    ; OFF track offset - first track has OS on it
    ; disk size = 8192 Kb
; doesn't work
; dpblk:
;     dw $0100    ; SPT sectors per track
;     db $04      ; BSH block shift factor
;     db $0f      ; BLM block mask
;     db $00      ; EXM extent mask
;     dw $7fff    ; DSM disk size - 1
;     dw $0100    ; DRM directory max
;     db $f0      ; AL0 alloc 0
;     db $00      ; AL1 alloc 1
;     dw $0000    ; CKS check size - number of dir entries to check to see
;                 ;     if disk has changed. Treat this as a fixed disk for now.
;     dw $0002    ; OFF track offset - first track has OS on it
;     ; disk size = 65536 Kb

dpb_disk0:
    dw $0000,$0000
    dw $0000,$0000
    dw dirbf,dpblk
    dw chk00,all00

response: db 0, 0, 0, 0, 0

cmd0:
    db $40,0,0,0,0,$95
cmd8:
    db $48,0,0,1,$AA,$87    ; 2.7-3.6V
acmd:
    db $77,0,0,0,0,$01   ; cmd55 - next command is and acmd
acmd41:
    db $69,$40,0,0,0,$01
cmd58:
    db $7A,0,0,0,0,$01


cmd_read_block:  ; read single block
    db $51,0,0,0,0,$01      ; needs a data address
cmd_write_block:  ; write single block
    db $58,0,0,0,0,$01

buffer: ds 512

sd_send_command_slow:
.block
    ld b,6
    ld c,sd_data
send_loop:
    ld a,(hl)
    call sd_write_a
    inc hl
    djnz send_loop
    call sd_read_response
    ret
.endblock

sd_send_command:
.block
    ld b,6
    ld c,sd_data
send_loop:
    otir
    call sd_read_response
    ret
.endblock

sd_write_a:
.block
    ex af,af'
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait
    ex af,af'
    out (sd_data),a
    ret
.endblock

; b = number of bytes to read
; hl = address to store bytes
sd_read_bytes:
.block
    ld a,$ff
    call sd_write_a
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait

    in a,(sd_data)
    ld (hl),a
    inc hl
    djnz sd_read_bytes
    ret
.endblock

; b = number of bytes to write
; hl = address of bytes to write
sd_write_bytes:
    ld c,sd_data
    otir
    ret

; reads an r1 response
sd_read_response:
.block
    ld a,$ff
    call sd_write_a
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait

    in a,(sd_data)
    bit 7,a
    jp nz,sd_read_response
    ret
.endblock

; reads a data response
sd_read_data_response:
.block
    ld a,$ff
    call sd_write_a
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait

    in a,(sd_data)
    and $11
    cp 1
    jp nz,sd_read_data_response
    ret
.endblock

sd_wait_for_non_busy_token:
.block
    ld a,$ff
    call sd_write_a
wait:
    in a,(sd_status)
    bit 2,a
    jp nz,wait

    in a,(sd_data)
    cp 0
    jp z,sd_wait_for_non_busy_token
    ret
.endblock

dirbf: ds 128
chk00: ds 16
all00: ds 31

test:
    ld bc,0
    call settrk
    ld bc,0
    call setsec
    ld bc,$9000
    call setdma
    ld c,0
    call seldsk
    call read
    ret

bell: equ 6
bell_on: equ $ff
bell_off: equ 0

sound_bell:
    push bc                     ; set up loop variables
    ld bc,$0010
    ld a,bell_on
    out (bell),a                ; turn the bell on
sound_bell1:
    djnz sound_bell1            ; do the b loop
    dec c
    jp nz,sound_bell1           ; do the c loop
    ld a,bell_off
    out (bell),a                ; turn the bell off
    pop bc
    ret

fill_disk:
    ld hl,$9000
    ld (hl),$e5
    ld de,$9001
    ld bc,512
    ldir

    ld bc,0
    ld de,0
fill_disk_next:
    push bc
    push de

    ld a,d
    ld (cmd_write_block + 1),a
    ld a,e
    ld (cmd_write_block + 2),a
    ld a,b
    ld (cmd_write_block + 3),a
    ld a,c
    ld (cmd_write_block + 4),a
    
    sd_set_cs 2
    ld hl,cmd_write_block
    call sd_send_command

    ld a,$fe  ; start block token
    call sd_write_a

    ld b,0
    ld hl,$9000
    call sd_write_bytes

    ld b,0
    ld hl,$9000
    call sd_write_bytes

    call sd_read_data_response
    ld (response),a

    call sd_wait_for_non_busy_token

    sd_set_cs 3

    pop de
    pop bc
    inc bc
    xor a
    cp b
    jp nz,fill_disk_next
    cp c
    jp nz,fill_disk_next
    inc de
    cp d
    jp nz,fill_disk_next
    cp e
    jp nz,fill_disk_next
    ret

; Load memory at 8000 to 100 and then start CP/M
; Then from CP/M you can do a SAVE nn filename
; in order to save this data (up to 4Kb) into a file
load_and_boot:
    ld hl,$8000
    ld de,$0100
    ld bc,$4000
    ldir
    jp boot
end: